apply plugin: 'com.android.application'
apply plugin: 'io.objectbox'

android {


    compileSdkVersion 27
    def BUILD_DATE = new Date().format('yyyy_MM_dd_HHmm')
    def versionPropsFile = file('version.properties')
    def versionBuild
    def versionMajor
    def versionMinor
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger()
        versionMajor = versionProps['VERSION_MAJOR'].toInteger()
        versionMinor = versionProps['VERSION_MINOR'].toInteger()
    } else {
        throw new FileNotFoundException("Could not read version.properties!")
    }
    ext.autoIncrementBuildNumber = {

        if (versionPropsFile.canRead()) {
            def Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
            versionProps['VERSION_BUILD'] = versionBuild.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        } else {
            throw new FileNotFoundException("Could not read version.properties!")
        }
    }




    defaultConfig {
        applicationId "com.disarm.surakshit.pdm"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode versionMajor * 10000 + versionMinor * 100 + versionBuild
        versionName versionMajor.toString() + "." + versionMinor.toString() + "." + versionBuild + "." + BUILD_DATE
        multiDexEnabled true
    }
    buildTypes {
        debug {
            versionNameSuffix ".debug"
            resValue "string", "app_version",
                    "${defaultConfig.versionName}${versionNameSuffix}"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "Surakshit_New_UI_" + defaultConfig.getVersionName() + ".apk"
                }
            }
        }
    }
    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(assembleDebug)) {  /* when run debug task */
            autoIncrementBuildNumber()
        } else if (taskGraph.hasTask(assembleRelease)) { /* when run release task */
            autoIncrementBuildNumber()
        }
    }
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //Support Library
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:preference-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'org.apache.commons:commons-lang3:3.6'
    //PSync
    implementation files('libs/psync-pc-jar-with-dependencies.jar')

    //Diff
    implementation files('libs/jbdiff.jar')

    //OSMDroid
    implementation 'org.osmdroid:osmdroid-android:6.0.1'
    //OSMBonusPack
    implementation(name: 'osmbonuspack_6.5.1', ext: 'aar')

    //Chat UI Library - ChatKit
    implementation 'com.github.stfalcon:chatkit:0.2.2'
    //Image Editor used in ChatKit
    implementation 'com.squareup.picasso:picasso:2.71828'

    //PGP Encryption - Bouncy Castle
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.59'
    implementation group: 'org.bouncycastle', name: 'bcpg-jdk15on', version: '1.59'

    //Contact Library
    implementation 'com.1gravity:android-contactpicker:1.3.2'
    implementation 'com.github.tamir7.contacts:contacts:1.1.7'

    //Button Library
    implementation 'com.github.jd-alexander:android-flat-button:v1.1'
    implementation 'com.getbase:floatingactionbutton:1.10.1'

    //EditText
    implementation 'com.github.florent37:materialtextfield:1.0.7'

    //Material Dialog
    implementation 'com.github.javiersantos:MaterialStyledDialogs:2.1'

    //Image initial library
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'

    //Video Player
    implementation 'com.afollestad:easyvideoplayer:0.3.0'

    //Image View
    implementation 'com.alexvasilkov:gesture-views:2.4.1'

    //Layout for attachments
    implementation 'com.google.android:flexbox:0.3.2'

    //SeekBar
    implementation 'com.annimon:paperstyle:1.1.0'

    //storage
    implementation 'com.snatik:storage:2.1.0'

    //IO
    // https://mvnrepository.com/artifact/commons-io/commons-io
    implementation group: 'commons-io', name: 'commons-io', version: '2.4'


    testImplementation 'junit:junit:4.12'
}
